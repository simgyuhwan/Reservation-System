plugins {
    id 'java'
    id 'org.springframework.boot' version '3.0.5'
    id 'io.spring.dependency-management' version '1.1.0'
}

// 루트 프로젝트는 main이 없기 때문에 제외
bootJar.enabled = false

allprojects {
    group = 'com.example'
    version = '0.0.1-SNAPSHOT'
    sourceCompatibility = '17'

    repositories {
        mavenCentral()
    }
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'io.spring.dependency-management'
    apply plugin: 'org.springframework.boot'

    dependencies {
        testImplementation 'org.springframework.boot:spring-boot-starter-test'
    }

    configurations {
        compileOnly {
            extendsFrom annotationProcessor
        }
    }

    tasks.withType(JavaCompile) {
        options.encoding = "UTF-8"
    }
}

project(":common") {
    dependencies {
        implementation(project(":config"))
    }
    bootJar { enabled = false }
    jar { enabled = true }
}

project(":config") {
    dependencies {
        // open-api
        implementation group: 'org.springdoc', name: 'springdoc-openapi-starter-webmvc-ui', version: '2.0.0'
    }
    bootJar { enabled = false }
    jar { enabled = true }
}

project(":memberservice") {
    dependencies {
        implementation(project(':common'))
        runtimeOnly 'com.mysql:mysql-connector-j'
    }

    tasks.named('test') {
        useJUnitPlatform()
    }
}

project(':performanceservice') {
    dependencies {
        implementation(project(':common'))
        runtimeOnly 'com.mysql:mysql-connector-j'
    }

    tasks.named('test') {
        useJUnitPlatform()
    }
}

project(':discoveryserver') {
    tasks.named('test') {
        useJUnitPlatform()
    }
}

project(':gatewayserver') {
    tasks.named('test') {
        useJUnitPlatform()
    }
}
